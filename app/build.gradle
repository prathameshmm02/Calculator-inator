apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.inator.calculator"
        minSdk 21
        targetSdk 33
        versionCode 6
        versionName "1.2.0"
    }

    signingConfigs {
        debug {
                // Nothing here
            }

        release {
            if (rootProject.file("secure.properties").exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(rootProject.file("secure.properties")))

                storeFile file(props['key.store'])
                keyAlias props['key.alias']
                storePassword props['key.storepassword']
                keyPassword props['key.keypassword']
            } else {
                // Nothing here
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (rootProject.file("secure.properties").exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lint {
        warning 'MissingTranslation'
    }
    namespace 'com.inator.calculator'
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appCompatVersion"

    // Room components
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // UI
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "com.google.android.material:material:$materialVersion"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Shared preferences
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // MxParser
    implementation 'org.mariuszgromada.math:MathParser.org-mXparser:5.0.7'
}